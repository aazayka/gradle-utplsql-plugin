apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/sonar.gradle"
apply from: "$rootDir/gradle/groovydoc.gradle"

group = 'com.iadams'
version = '0.1'

description = "Gradle plugin for utplsql test framework"

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}
dependencies {
    compile localGroovy()
    compile gradleApi()

    testCompile 'xmlunit:xmlunit:1.3'
    testCompile 'com.netflix.nebula:nebula-test:1.12.+'
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.html.destination = file("$buildDir/reports/integ")
}

integTest.mustRunAfter test
check.dependsOn integTest

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn test, integTest
}

task jacocoIntegTestReport(type: JacocoReport) {
    sourceSets sourceSets.main
    executionData integTest
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacocoIntegTestReport"
    }
}